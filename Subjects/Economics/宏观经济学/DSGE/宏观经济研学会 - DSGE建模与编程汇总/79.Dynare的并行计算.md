#类型/教程

#知识 

#资料 

#来源/转载



[[宏观经济]]

[[DSGE]]

[[DSGE编程]]



本文参考Mutschler的“Dynare Parallel” 和Dynare Manual的相关内容。本文使用的dynare code见文末，或者去“量化经济分析平台”www.econmod.cn下载。本文演示的平台为Windows（MacOS和Lunix可以发邮件与我交流）。





演示系统环境：Win10、Matlab2020a、dynare 4.6.1







正

文







在Dynare中，所有的任务通常都是单线程。这样会极大的浪费资源，更重要的是运行速度慢！例如，我的电脑是4核的，当我在使用estimation命令时，它只会一条链一条链的来运行，而且只在一个CPU上运行，其它3个CPU则在一旁吹风。这时候，我们可能想，要是运行4条Markov Chains的时候，每条链分别在一个CPU上同时运行，那速度肯定快很多。确实，并行计算就可以解决这个问题。Dynare使用并行计算的命令为

```octave
dynare mymodel.mod parallel
```

许多Matlab\Octave程序在本地计算机或者远程服务器上都可以完成并行计算。主程序和附属程序是通过Windows上的SMB或者UNIX上的SSH完成的。而输入和输出数据，以及一些简短的状态信息都是通过文件系统的传输来完成的。目前，并行计算系统仅仅可在同一平台上运行：要么Windows，要么UNIX的设备。同时，Dynare的下列步骤可以由并行计算完成：

- 当我们使用多链条时，后验抽样算法可以并行计算；
- MH诊断
- 后验IRFs
- 先验和后验统计量
- 一些作图程序

**下面，我们就介绍一下Dynare利用并行计算的一些系统配置。**



**第一步：配置PSTools**

1. 下载PSTools：https://docs.microsoft.com/en-us/sysinternals/downloads/pstools
1. 解压到任何文件夹，例如 C:\App\PSTools
1. 将上述文件路径放入系统用户自定义环境PATH中
1. 找到“计算机”，点击右键-属性-高级系统设置-高级-环境变量(N)...-用户变量-PATH-编辑-新建-输入PSTools路径-确定

![Image](640-20210302194936723.png)

![Image](640-20210302194936052)

![Image](640-20210302194936048)

![Image](640-20210302194936047)

5.打开命令提示符，输入psexec和psinfo，同意弹出窗口。

注意：psinfo会在界面输出计算机的信息，例如，4个CPUs等。记住这个信息。

![Image](640-20210302194936069)



**第二步：配置文件**

注意，配置文件中的评论以#开始。



```shell
#cluster needs to always be defined first
[cluster]
#Provide a name for the cluster
Name=Local
#declare the nodes being member of the cluster
Members=n1


#declare nodes (they need not all be part of a cluster)
[node]
#name of the node
Name=n1
#name of the computer (localhost for the current machine)
ComputerName=localhost
#cores to be included from this node
CPUnbr=[1:4]
#path to matlab.exe; on Windows, the MATLAB bin folder is in the system path
#so we only need to provide the name of the exe file
MatlabOctavePath=matlab
#Dynare path you are using
DynarePath=C:/App/5.6.1/matlab
```

在上述配置文件中，[cluster]中只包含一个计算机成员n1，这是因为我只有一台笔记本。如果我们有多个计算机，我们需要将其全部包含在内。

在[node]模块，我们要为一些选项。Name必须与cluster模块中member对应。如果member中有多个计算机成员，node模块也要相应声明。由于我这里使用自己的本地计算机，没有使用远程计算机，因此，选择localhost。我的计算机是4核的，因此，我选择了1:4（当然，也可以直接赋值4）。将其保存在任意文件夹（***但是一定要记住这个文件路径***），且文件后缀改为***.ini，例如，myconf.ini。\***





**第三步：运行dynare并行计算**

首先，在matlab上调用上述配置文件myconf.ini来测试dynare并行计算是否配置成功：

```octave
dynare mymodel parallel_test conffile='C:\Users\xuwen\Desktop\test\Dyn_parallel\myconf.ini'
```



如果一切配置成功，matlab会显示如下结果

![Image](640-20210302195031450)



那么，我们就可以进行dynare的并行估计过程了：

```octave
dynare mymodel parallel conffile='C:\Users\xuwen\Desktop\test\Dyn_parallel\myconf.ini'
```



matlab就会利用我的4核计算机来并行估计一个DSGE模型的参数。



如果我们每次都是使用相同的配置文件，我们看到，我们调用配置文件都需要记住其保存的路径。有一种方法一劳永逸：我们将myconf.ini复制粘贴到C:\Users\xuwen\AppData\Roaming，并重命名为dynare.ini。这个时候，我们就可以不用conffile选项命令了:

```octave
dynare mymodel parallel
```



这个时候，我们就会很快看到如下并行估计的界面

![Image](640-20210302195112684)



而且并行估计的时间真的非常短。





附代码：

```octave
var YGR INFL INT y c r p g z;
varobs YGR INFL INT;
varexo epsr epsg epsz;

parameters TAU KAPPA PSI1 PSI2 RHOR RHOG RHOZ RA PA GAMQ;

TAU     = 2.00; PSI1    = 1.50; RHOR    = 0.60;
KAPPA   = 0.15; PSI2    = 1.00; RHOG    = 0.95;
RA      = 0.40; GAMQ    = 0.50; RHOZ    = 0.65;
PA      = 4.00;

model;
  #dy = y - y(-1);
  #BET = 1/(1+RA/400);
  y = y(+1) + g - g(+1) -1/TAU*(r - p(+1) - z(+1));
  p = BET*p(+1) + KAPPA*(y - g);
  c = y - g;
  r = RHOR*r(-1) + (1-RHOR)*PSI1*p + (1-RHOR)*PSI2*(dy + z) + epsr/100;
  g = RHOG*g(-1) + epsg/100;
  z = RHOZ*z(-1) + epsz/100;
  YGR = GAMQ + 100*(y - y(-1) + z);
  INFL = PA + 400*p;
  INT = PA + RA + 4*GAMQ + 400*r;
end;

steady_state_model;
  z = 0; p = 0; g = 0; r = 0; c = 0; y = 0;
  YGR = GAMQ; INFL = PA; INT = PA + RA + 4*GAMQ;
end;

shocks;
  var epsr = 0.20^2;
  var epsg = 0.80^2;
  var epsz = 0.45^2;
end;

estimated_params;
  TAU,             2.00,          1e-5,        10,          gamma_pdf,     2.00,       0.50;
  KAPPA,           0.15,          1e-5,        10,          gamma_pdf,     0.20,       0.10;
  PSI1,            1.50,          1e-5,        10,          gamma_pdf,     1.50,       0.25;
  PSI2,            1.00,          1e-5,        10,          gamma_pdf,     0.50,       0.25;
  RHOR,            0.60,          1e-5,        0.99999,     beta_pdf,      0.50,       0.20;
  RHOG,            0.95,          1e-5,        0.99999,     beta_pdf,      0.80,       0.10;
  RHOZ,            0.64,          1e-5,        0.99999,     beta_pdf,      0.66,       0.15;
  RA,              0.40,          1e-5,        10,          gamma_pdf,     0.50,       0.50;
  PA,              4.00,          1e-5,        20,          gamma_pdf,     7.00,       2.00;
  GAMQ,            0.50,          -5,          5,           normal_pdf,    0.40,       0.20;
  stderr epsr,     0.20,          1e-8,        5,           inv_gamma_pdf, 0.50,       0.26;
  stderr epsg,     0.80,          1e-8,        5,           inv_gamma_pdf, 1.25,       0.65;
  stderr epsz,     0.45,          1e-8,        5,           inv_gamma_pdf, 0.63,       0.33;
end;

model_diagnostics; steady; check;

stoch_simul(order=1,IRF=0,periods=10000);
save('simdat.mat', options_.varobs{:} );

estimation(datafile='simdat.mat',
           first_obs                 = 5001,
           nobs                      = 100,
           mode_compute              = 4,
           mcmc_jumping_covariance   = hessian,
           mh_replic                 = 2001,
           mh_nblocks                = 4,
           mh_jscale                 = 0.4,
           posterior_sampling_method = 'random_walk_metropolis_hastings',
           posterior_sampler_options = ('proposal_distribution', 'rand_multivariate_student',
                                      'student_degrees_of_freedom', 3)
           );
```













