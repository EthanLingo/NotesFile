#类型/教程

#知识 

#资料 

#来源/转载



[[宏观经济]]

[[DSGE]]

[[DSGE方法]]






    注：历史和平滑变量图、历史方差分解图本来是第51期“DSGE建模与编程入门”。但好多人给我发邮件、留言问这个怎么做。所以今天就提前发出来吧。之前已经推送了40期，可以回复“DSGE”查看往期汇总。中间还有41-50也会陆续推送，敬请期待。



​    ***1、\******历史和平滑变量图\***是***estimation***命令产生的图，且***estimation（）\***命令选项中没有***smoother***选项。下面以dyanre中自带的例子**fs2000.mod**来说明：



------

**var** m P c e W R k d n l gy_obs gp_obs y dA;

**varexo** e_a e_m;

**……**

**……**

**varobs** gp_obs gy_obs;



**estimation**(order=1, datafile=fsdat_simul, nobs=192, loglinear, mh_replic=2000, mh_nblocks=2, mh_jscale=0.8);

------



​    即上面的***estimation（）\***命令红色选项中没有***smoother***选项。就可以产生如下图：

![Image](640-20210302160732271)

图1 历史和平滑变量图



​       **黑色线条**描述的是实际观测数据，而红色线条描述的是平滑变量的估计值。如果观测方程中不包含测量误差，那么实际序列和估计序列是一样的，反之则会存在差异。



​    2、***历史方差分解***是在***estimation***或者***stoch_simul***命令之后使用的。它的命令形式为



------

shock_decomposition [VARIABLE_NAME]. . . ; 

或者

shock_decomposition (OPTIONS. . . ) [VARIABLE_NAME]. . . ;



描述

该命令基于Kalman smoother计算给定样本的历史冲击分解。即将每个内生变量的历史稳态偏离程度分解成不同冲击的贡献率。

------



​    我们还是基于上述**fs2000**的例子：



------

**var** m P c e W R k d n l gy_obs gp_obs y dA;

**varexo** e_a e_m;

**……**

**……**

**varobs** gp_obs gy_obs;



**estimation**(order=1, datafile=fsdat_simul, nobs=192, loglinear, mh_replic=0, mh_nblocks=2, mh_jscale=0.8);



**shock_decomposition**;

------



​    对比一下历史方差分解命令和上面的fs2000估计命令，可以清晰地看出，***estimation（）***前的所有命令都不变，只是在estimation之后加了一行代码：***shock_decomposition*****。**

​    **注：上述命令会产生所有内生变量的历史冲击方差分解图。如果只需要我们感兴趣的内生变量，那么，在\**shock_decomposition后面加上内生变量名，例如，\*\*shock_decomposition y W\*\*;\**** 

***\*
\****

产生了如下历史分解图（以y为例）：

![Image](640-20210302160732293)

图2 产出的历史冲击分解



​    **黑线**描述了内生变量的平滑值偏离稳态程度。彩色的柱状图表示每种平滑冲击对偏离的贡献程度，例如，蓝色柱状表示技术冲对产出稳态偏离的贡献率，绿色柱状表示货币冲击的贡献，灰色柱状表示未被解释的部分。



下面给出历史方差分解的完整命令：



```octave
var m P c e W R k d n l gy_obs gp_obs y dA;
varexo e_a e_m;

parameters alp bet gam mst rho psi del;

alp = 0.33;
bet = 0.99;
gam = 0.003;
mst = 1.011;
rho = 0.7;
psi = 0.787;
del = 0.02;

model;
dA = exp(gam+e_a);
log(m) = (1-rho)*log(mst) + rho*log(m(-1))+e_m;
-P/(c(+1)*P(+1)*m)+bet*P(+1)*(alp*exp(-alp*(gam+log(e(+1))))*k^(alp-1)*n(+1)^(1-alp)+(1-del)*exp(-(gam+log(e(+1)))))/(c(+2)*P(+2)*m(+1))=0;
W = l/n;
-(psi/(1-psi))*(c*P/(1-n))+l/n = 0;
R = P*(1-alp)*exp(-alp*(gam+e_a))*k(-1)^alp*n^(-alp)/W;
1/(c*P)-bet*P*(1-alp)*exp(-alp*(gam+e_a))*k(-1)^alp*n^(1-alp)/(m*l*c(+1)*P(+1)) = 0;
c+k = exp(-alp*(gam+e_a))*k(-1)^alp*n^(1-alp)+(1-del)*exp(-(gam+e_a))*k(-1);
P*c = m;
m-1+d = l;
e = exp(e_a);
y = k(-1)^alp*n^(1-alp)*exp(-alp*(gam+e_a));
gy_obs = dA*y/y(-1);
gp_obs = (P/P(-1))*m(-1)/dA;
end;

shocks;
var e_a; stderr 0.014;
var e_m; stderr 0.005;
end;

steady_state_model;
  dA = exp(gam);
  gst = 1/dA;
  m = mst;
  
  khst = ( (1-gst*bet*(1-del)) / (alp*gst^alp*bet) )^(1/(alp-1));
  xist = ( ((khst*gst)^alp - (1-gst*(1-del))*khst)/mst )^(-1);
  nust = psi*mst^2/( (1-alp)*(1-psi)*bet*gst^alp*khst^alp );
  n  = xist/(nust+xist);
  P  = xist + nust;
  k  = khst*n;

  l  = psi*mst*n/( (1-psi)*(1-n) );
  c  = mst/P;
  d  = l - mst + 1;
  y  = k^alp*n^(1-alp)*gst^alp;
  R  = mst/bet;

  W = l/n;
  e = 1;

  gp_obs = m/dA;
  gy_obs = dA;
end;

steady;

check;

estimated_params;
alp, beta_pdf, 0.356, 0.02;
bet, beta_pdf, 0.993, 0.002;
gam, normal_pdf, 0.0085, 0.003;
mst, normal_pdf, 1.0002, 0.007;
rho, beta_pdf, 0.129, 0.223;
psi, beta_pdf, 0.65, 0.05;
del, beta_pdf, 0.01, 0.005;
stderr e_a, inv_gamma_pdf, 0.035449, inf;
stderr e_m, inv_gamma_pdf, 0.008862, inf;
end;

varobs gp_obs gy_obs;

estimation(order=1,datafile=fsdat_simul,nobs=192,loglinear,mh_replic=0)y W;

shock_decomposition y W;
```





